

program : statement_list

statement_list : statement (COMMA statement)*

statement : var_decl
          | var_set
          | function_decl
          | if_statement
          | while_statement
          | RETURN conditional
          | built_in_function

var_decl : conditional

var_set : SET (PERIOD)* INT_CONST SET_TO conditional

function_decl : (INT_CONST)? COLON statement_list SEMI

if_statement : IF conditional COLON statement_list SEMI (ELSE (if_statement | COLON statement_list SEMI))?

while_statement : WHILE conditional COLON statement_list SEMI

conditional : and_condition (OR and_condition)*

and_condition : condition (AND condition)*

condition : (NOT)? LPAREN conditional RPAREN
          | expr ((EQUAL | NOT_EQUAL | LTHAN | GTHAN | LTHAN_OR_EQUAL | GTHAN_OR_EQUAL) expr)*

expr : term ((PLUS | MINUS) term)*

term : factor ((MUL | INT_DIV | FLOAT_DIV | MOD) factor)*

// Add Exponents

factor : PLUS factor
       | MINUS factor
       | INT_CONST
       | FLOAT_CONST
       | STR_CONST
       | LPAREN conditional RPAREN
       | variable
       | built_in_function

variable : MEMORY (PERIOD)* INT_CONST (LANGLE (conditional)? (COMMA conditional)* RANGLE)?

built_in_function : PRINT LANGLE conditional (COMMA conditional)* RANGLE
                  | INPUT LANGLE conditional RANGLE
                  | RANDOM_INT LANGLE conditional COMMA conditional RANGLE
                  | CS LANGLE conditional RANGLE
                  | CI LANGLE conditional RANGLE
                  | CF LANGLE conditional RANGLE
                  | CB LANGLE conditional RANGLE