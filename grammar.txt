

program : statement_list

statement_list : statement (COMMA statement)*

statement : var_decl
          | var_set
          | function_decl
          | if_statement
          | while_statement
          | RETURN LANGLE conditional RANGLE
          | built_in_function
          | SET_TO conditional
          | import_file

import_file : IMPORT STR_CONST

var_decl : conditional

var_set : SET (PERIOD)* INT_CONST SET_TO conditional

function_decl : (INT_CONST)? COLON statement_list SEMI

if_statement : IF conditional COLON statement_list SEMI (ELSE (if_statement | COLON statement_list SEMI))?

while_statement : WHILE conditional COLON statement_list SEMI

conditional : and_condition (OR and_condition)*

and_condition : condition (AND condition)*

condition : (NOT)? LANGLE conditional RANGLE
          | expr ((EQUAL | NOT_EQUAL | LTHAN | GTHAN | LTHAN_OR_EQUAL | GTHAN_OR_EQUAL) expr)*

expr : term ((PLUS | MINUS) term)*

term : expo ((MUL | INT_DIV | FLOAT_DIV | MOD) expo)*

expo : factor ((EXPO) factor)*

factor : PLUS factor
       | MINUS factor
       | INT_CONST
       | FLOAT_CONST
       | STR_CONST
       | BOOL_CONST
       | LANGLE conditional RANGLE
       | variable
       | module
       | built_in_function

variable : MEMORY (PERIOD)* INT_CONST (LANGLE (conditional)? (COMMA conditional)* RANGLE)?

module : MODULE (PERIOD)* INT_CONST variable

built_in_function : FUNCTION LANGLE conditional (COMMA conditional)* RANGLE