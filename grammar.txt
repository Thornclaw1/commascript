

program : statement_list

statement_list : statement (COMMA statement)* (COMMA)?

statement : var_decl
          | var_set
          | function_decl
          | macro_decl
          | if_statement
          | while_statement
          | for_statement
          | RETURN (LANGLE conditional RANGLE)?
          | BREAK
          | CONTINUE
          | built_in_function
          | SET_TO conditional
          | NULL SET_TO condintional
          | import_file
          | open_file

import_file : IMPORT (PYTHON)? STR_CONST

open_file : (FILE_READ | FILE_WRITE | FILE_APPEND) STR_CONST COLON statement_list SEMI

var_decl : conditional

var_set : SET (PERIOD)* (MINUS)? INT_CONST ((SET_TO | PLUS | MINUS) conditional | (PLUS_PLUS | MINUS_MINUS))

function_decl : (INT_CONST)? (PARAM_SEP (condintional)? (COMMA condintional)*)? COLON statement_list SEMI

macro_decl : RANGLE COLON statement (COMMA statement)* (COMMA)? SEMI

if_statement : IF conditional COLON statement_list SEMI (ELSE (if_statement | COLON statement_list SEMI))?

while_statement : WHILE conditional COLON statement_list SEMI

for_statement : FOR conditional COLON statement_list SEMI

conditional : and_condition (OR and_condition)*

and_condition : condition (AND condition)*

condition : (NOT)? LANGLE conditional RANGLE
          | expr ((EQUAL | NOT_EQUAL | LTHAN | GTHAN | LTHAN_OR_EQUAL | GTHAN_OR_EQUAL) expr)*

expr : term ((PLUS | MINUS) term)*

term : expo ((MUL | INT_DIV | FLOAT_DIV | MOD) expo)*

expo : factor ((EXPO) factor)*

factor : PLUS factor
       | MINUS factor
       | INT_CONST
       | FLOAT_CONST
       | STR_CONST
       | BOOL_CONST
       | list 
       | tuple
       | dictionary
       | LANGLE conditional RANGLE
       | variable
       | module
       | built_in_function
       | NULL

list : LBRACKET (conditional)? (COMMA conditional)* (COMMA)? RBRACKET

tuple : LPAREN (condintional)? (COMMA condintional)* (COMMA)? RPAREN

dict : LCURLY (conditional COLON conditional)? (COMMA conditional COLON conditional)* (COMMA)? RCURLY

variable : MEMORY (PERIOD)* (MINUS)? INT_CONST (LANGLE (conditional)? (COMMA conditional)* RANGLE | LBRACKET conditional RBRACKET)?

module : MODULE (PERIOD)* (MINUS)? INT_CONST (variable | python_module_function)

built_in_function : (PYTHON)? FUNCTION LANGLE (conditional)? (COMMA conditional)* RANGLE

python_module_function : FUNCTION (LANGLE (conditional)? (COMMA conditional)* RANGLE)?